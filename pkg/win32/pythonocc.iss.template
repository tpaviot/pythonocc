;Copyright 2008-2013 Thomas Paviot (tpaviot@gmail.com)
;
;This file is part of pythonOCC.
;
;pythonOCC is free software: you can redistribute it and/or modify
;it under the terms of the GNU Lesser General Public License as published by
;the Free Software Foundation, either version 3 of the License, or
;(at your option) any later version.
;
;pythonOCC is distributed in the hope that it will be useful,
;but WITHOUT ANY WARRANTY; without even the implied warranty of
;MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;GNU Lesser General Public License for more details.
;
;You should have received a copy of the GNU Lesser General Public License
;along with pythonOCC.  If not, see <http://www.gnu.org/licenses/>.


[Setup]
AppName=pythonOCC-@PYTHONOCC_VERSION@
AppVerName=pythonOCC-@PYTHONOCC_VERSION@
AppVersion=@PYTHONOCC_VERSION@
AppPublisher=Thomas Paviot
AppCopyright=Copyright 2008-2013 Thomas Paviot
AppPublisherURL=http://www.pythonocc.org
AppSupportURL=http://www.pythonocc.org
AppUpdatesURL=http://www.pythonocc.org
DefaultDirName={code:GetPythonSitePackageDir}\OCC
DefaultGroupName=pythonOCC
LicenseFile=@PYTHONOCC_SRC_DIR@\LICENSE

;Compression parameters
;lzma or zip
Compression=lzma/ultra64
SolidCompression=yes
;AdminPrivilegesRequired=no
PrivilegesRequired=admin
OutputBaseFilename=pythonOCC-@PYTHONOCC_VERSION@-win32-py@PYTHON_VERSION_NO_DOT@
OutputDir=@PYTHONOCC_PKG_DIR@
WindowResizable=false
WindowStartMaximized=true
WindowShowCaption=true
BackColorDirection=lefttoright
ChangesEnvironment=yes

[Languages]
Name: en; MessagesFile: "compiler:Default.isl"

[Messages]
BeveledLabel=pythonOCC-@PYTHONOCC_VERSION@ for Win32/Python@PYTHON_VERSION_NO_DOT@ installation

[Files]
Source: C:\Python@PYTHON_VERSION_NO_DOT@\Lib\site-packages\OCC\*.*; Excludes: "*.pyc"; DestDir: {app}; Flags : recursesubdirs;
;
; Misc files
;
Source: @PYTHONOCC_SRC_DIR@\LICENSE; DestDir: {app}; Flags : ignoreversion;
Source: @PYTHONOCC_SRC_DIR@\AUTHORS; DestDir: {app}; Flags : ignoreversion;
Source: @PYTHONOCC_SRC_DIR@\CHANGELOG; DestDir: {app}; Flags : ignoreversion;

[Registry]
; set CSF_GraphicShr
Root: HKCU; Subkey: "Environment"; ValueType:string; ValueName:"CSF_GraphicShr"; ValueData:"C:\Python@PYTHON_VERSION_NO_DOT@\Lib\site-packages\OCC\TKOpenGl.dll"

[_ISTool]
Use7zip=true

[code]
program Setup;
var
    PythonDir  : String;
    InstallDir : String;

function InitializeSetup(): Boolean;
begin

    if not RegQueryStringValue(HKEY_CURRENT_USER,
            'Software\Python\PythonCore\@PYTHON_VERSION@\InstallPath',
            '', PythonDir) then begin

        if not RegQueryStringValue(HKEY_LOCAL_MACHINE,
                'Software\Python\PythonCore\@PYTHON_VERSION@\InstallPath',
                '', PythonDir) then begin

            if not RegQueryStringValue(HKEY_CURRENT_USER,
                    'Software\Wow6432Node\Python\PythonCore\@PYTHON_VERSION@\InstallPath',
                    '', PythonDir) then begin

                if not RegQueryStringValue(HKEY_LOCAL_MACHINE,
                        'Software\Wow6432Node\Python\PythonCore\@PYTHON_VERSION@\InstallPath',
                        '', PythonDir) then begin

                    MsgBox('No installation of Python @PYTHON_VERSION@ '
                           + 'found in registry.' + #13 + 'Be sure to enter '
                           + 'a pathname that places pythonocc on the '
                           + 'PYTHONPATH',
                           mbConfirmation, MB_OK);
                end;
            end;
        end;
    end;

    InstallDir := PythonDir + '\Lib\site-packages';
    Result := True;
end;

function GetPythonSitePackageDir(Default: String): String;
begin
    Result := InstallDir;
end;
